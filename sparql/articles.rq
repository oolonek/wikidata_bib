#defaultView:Table
SELECT DISTINCT ?compound ?compoundLabel ?inchikey ?inchi ?smiles_isomeric ?smiles_canonical ?cas ?chebi ?chembl ?pubchem ?taxon ?taxon_name ?taxon_id_gbif ?taxon_id_ncbi ?reference ?reference_doi ?reference_title WHERE {
  ?taxon wdt:P171 ?parent_taxon.
  ?parent_taxon wdt:P225 'Lespedeza'.
  ?compound wdt:P235 ?inchikey.
  OPTIONAL { ?compound wdt:P231 ?cas. }
  OPTIONAL { ?compound wdt:P233 ?smiles_canonical. }
  OPTIONAL { ?compound wdt:P234 ?inchi. }
  OPTIONAL { ?compound wdt:P592 ?chembl. }
  OPTIONAL { ?compound wdt:P662 ?pubchem. }
  OPTIONAL { ?compound wdt:P683 ?chebi. }
  OPTIONAL { ?compound wdt:P2017 ?smiles_isomeric. }
  {
    ?compound p:P703 ?statement.
    ?statement ps:P703 ?taxon.
    OPTIONAL { ?taxon wdt:P225 ?taxon_name. }
    OPTIONAL { ?taxon wdt:P846 ?taxon_id_gbif. }
    OPTIONAL { ?taxon wdt:P685 ?taxon_id_ncbi. }
    {
      ?statement prov:wasDerivedFrom ?ref.
      ?ref pr:P248 ?reference.
      ?reference wdt:P356 ?reference_doi;
        wdt:P1476 ?reference_title.
    }
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}

